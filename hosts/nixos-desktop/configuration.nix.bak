{
  config,
  pkgs,
  lib,
  nixos,
  ...
}:
{
  imports = [
    # Common krit modules
    # This import common system-wide modules
    ../../common/krit/modules/default.nix # General import (since desktop contains everything) (both system and home-manager) (do not incude NAS modules)
    ../../common/krit/modules/system/nas/default.nix # General NAS system modules
    # Local modules
    ./optional/default.nix
  ];

  # +++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  # üåç LOCALE
  # +++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  i18n.defaultLocale = "en_US.UTF-8";

  # +++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  # üîê SOPS CONFIGURATION
  # +++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  # 1. DEFAULT SOURCE (Host Specific)
  sops.defaultSopsFile = ./optional/host-sops-nix/nixos-desktop-secrets-sops.yaml;
  sops.defaultSopsFormat = "yaml";
  sops.age.sshKeyPaths = [ "/etc/ssh/ssh_host_ed25519_key" ];

  # 2. GLOBAL SECRETS DEFINITION
  sops.secrets =
    let
      commonSecrets = ../../common/krit/sops/krit-common-secrets-sops.yaml;
    in
    {
      # LOCAL SECRETS:
      # Loc-1. Local User password
      "krit-local-password".neededForUsers = true;

      # COMMON SECRETS:
      # Comm-3
      github_fg_pat_token_nix = {
        sopsFile = commonSecrets;
        mode = "0444";
      };
      # Comm-1
      github_general_ssh_key = {
        sopsFile = commonSecrets;
        owner = nixos.constants.user;
        path = "/home/${nixos.constants.user}/.ssh/id_github";
      };

      # Comm-4
      Krit_Wifi_pass = {
        sopsFile = commonSecrets;
        restartUnits = [ "NetworkManager.service" ];
      };
      Nicol_5Ghz_pass = {
        sopsFile = commonSecrets;
        restartUnits = [ "NetworkManager.service" ];
      };
      Nicol_2Ghz_pass = {
        sopsFile = commonSecrets;
        restartUnits = [ "NetworkManager.service" ];
      };

      # Comm-7. PGP signing key (git)
      commit_signing_key = {
        sopsFile = commonSecrets;
        owner = nixos.constants.user;
      };
    };

  # Tell Nix to read the Github token
  nix.extraOptions = ''
    !include ${config.sops.secrets.github_fg_pat_token_nix.path}
  '';

  # ---------------------------------------------------------
  # üë§ HOST-SPECIFIC GIT
  # ---------------------------------------------------------
  programs.git.enable = true; # Needed to make signing work since home-manager git module can't to it
  programs.git.config = {
    user.signingkey = "D93A24D8E063EECF";
    commit.gpgsign = true;
  };

  # Gnupg agent with pinentry-qt for graphical passphrase entry (required for git commits signing with gpg key)
  programs.gnupg.agent = {
    enable = true;
    enableSSHSupport = true;
    pinentryPackage = pkgs.pinentry-qt;
  };

  # SSH configuration to use the key for github access (Comm-1)
  programs.ssh = {
    extraConfig = ''
      Host github.com
      IdentityFile ${config.sops.secrets.github_general_ssh_key.path}
    '';
  };

  # ---------------------------------------------------------
  # ‚öôÔ∏è GRAPHICS & FONTS
  # ---------------------------------------------------------
  boot.initrd.kernelModules = [ "amdgpu" ];
  hardware.graphics.enable = true;

  /*
        boot.kernelParams = [
      "video=HDMI-A-2:1920x1080@60"
        ];
  */

  services.logind = {
    settings = {
      Login = {
        HandlePowerKey = "poweroff";
        HandlePowerKeyLongPress = "poweroff";
      };
    };
  };

  # ---------------------------------------------------------
  # üë§ USER CONFIGURATION
  # ---------------------------------------------------------
  users.mutableUsers = false; # Owerwrite manual password changes

  users.users.${nixos.constants.user} = {
    isNormalUser = true;
    description = "${nixos.constants.user}";
    extraGroups = [
      "networkmanager"
      "wheel"
      "input"
      "docker"
      "podman"
      "video"
      "audio"
    ];
    # Required for rootless Podman/Distrobox
    subUidRanges = [
      {
        startUid = 100000;
        count = 65536;
      }
    ];
    subGidRanges = [
      {
        startGid = 100000;
        count = 65536;
      }
    ];

    hashedPasswordFile = config.sops.secrets.krit-local-password.path;
  };

  # ---------------------------------------------------------
  # üê≥ VIRTUALIZATION & DOCKER
  # Needed because otherwise the group "docker" is not created
  # ---------------------------------------------------------
  virtualisation.docker.enable = true;

  # Limited mtu to make internet faster when enabled
  virtualisation.docker.daemon.settings = {
    "mtu" = 1450;
  };

  virtualisation.podman = {
    enable = true;
    dockerCompat = false; # Allows Podman to answer to 'docker' commands (false as it clash with docker)
  };

  # ---------------------------------------------------------
  # üõ°Ô∏è DNS PROTECTION (Quad9 + VPN Compat)
  # ---------------------------------------------------------
  services.resolved = {
    enable = true;
    dnssec = "false";
    domains = [ "~." ];
    fallbackDns = [
      "9.9.9.9"
      "149.112.112.112"
    ];
    dnsovertls = "opportunistic";
  };

  # ---------------------------------------------------------
  # üóëÔ∏è AUTO TRASH CLEANUP
  # ---------------------------------------------------------
  # 2. Define the cleanup service
  systemd.services.cleanup_trash = {
    description = "Clean up trash older than 30 days";
    serviceConfig = {
      Type = "oneshot";
      User = nixos.constants.user;
      Environment = "HOME=/home/${nixos.constants.user}";
      ExecStart = "${pkgs.autotrash}/bin/autotrash -d 30";
    };
  };

  # 3. Schedule it to run daily
  systemd.timers.cleanup_trash = {
    wantedBy = [ "timers.target" ];
    timerConfig = {
      OnCalendar = "daily"; # Runs once every 24h
      Persistent = true; # Run immediately if the computer was off during the scheduled time
    };
  };

  environment.systemPackages = with pkgs; [
    autotrash # Automatic trash cleanup
    docker # Required when virtualisation.docker.enable is true
    distrobox
    fd # User-friendly replacement for 'find'
    gnupg # Required for gpg key for settings git commits
    pinentry-qt # Required for gpg key for settings git commits
    pinentry-curses # Required for gpg key for settings git commits (fallback)
    libvdpau-va-gl # VDPAU backend for VAAPI (needed for video acceleration with AMD GPUs)
    logiops # Logitech devices manager (currently used for my MX Master 3S)
    pay-respects # Used in shell aliases dotfiles
    pokemon-colorscripts # Used in shell aliases dotfiles
    stow # Used to manage my dotfiles repo
    tree # Display directory structure as a tree
    unzip # Extraction utility for .zip files. It is used by programs to compress/decompress data.
    wget # Network downloader utility
    zip # Compression utility for .zip files. It is used by programs to compress/decompress data.
    zlib # Compression utility for .zip files. It is used by programs to compress/decompress data.
  ];
}
